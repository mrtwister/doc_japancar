# Функция ins. Добавление объявлений.

| Название параметра | Значение | Описание |
| :--- | :--- | :--- |
| s\_sn | набор символов длиной не более 255 символов. | код программы |
| p\_version | текст длиной не более 255 символов | версия программы |
| action | ins | действие выполняемое с данными |
| count | целое число от 1 до 500 | количество передаваемых запчастей в данном запросе |
| id\[n\] | набор символов длиной не более 255 символов. | уникальный номер запчасти в программе |
| code\[n\] | набор символов длиной не более 7 символов. | код запчасти |
| marka\[n\] | набор символов длиной не более 255 символов. | Марка авто |
| model\[n\] | набор символов длиной не более 20 символов. | Модель авто |
| modelN\[n\] | набор символов длиной не более 16 символов. | Номер запчасти в том числе и номер оптики |
| kuzovN\[n\] | набор символов длиной не более 16 символов. | модель кузова |
| engineN\[n\] | набор символов длиной не более 16 символов. | модель двигателя |
| F\_R\[n\] | -,R,F | Перед/Зад |
| U\_D\[n\] | -,U,D | Верх/Низ |
| R\_L\[n\] | -,L,R | Право/Лево |
| remark\[n\] | текст длиной не более 200 символов | Комментарий |
| price\[n\] | число длиной не более 16 знаков | Цена |
| price\_discount\[n\] | число длиной не более 16 знаков | Цена со скидкой \(при отправке указание параметра price - обязательно\) |
| id\_s\_typecurrency\[n\] | целое число до 11 знаков | Тип валюты |
| N\_O\[n\] | O,N | Новая/Контрактная |
| name\[n\] | текст длиной не более 64 символов | Название запчасти |
| producer\_code\[n\] | текст длиной не более 16 символов | Код производителя |
| producer\[n\] | текст длиной не более 32 символов | Производитель |
| oem\[n\] | текст длиной не более 16 символов | OEM |
| year\[n\] | текст длиной не более 55 символов | Год |
| photo\_count\[n\] | целое число до 11 знаков | Количество фотографий у запчасти |

Вид POST запроса:

```
s_sn=34itlwk87er9j&p_version=3.0.25&action=ins&count=40&id[0]=000000000&code[0]=%C4%C2%C8.091&marka[0]=NISSAN&model[0]=CEDRIC&modelN[0]=189250W&kuzovN[0]=CY31&engineN[0]=VG20-DET&F_R[0]=-&U_D[0]=-&R_L[0]=-&remark[0]=TEST%20!!!&price[0]=0%20%F0%F3%E1.&id_s_typecurrency[0]=4&N_O[0]=O&name[0]=%E4%E2%E8%E3%E0%F2%E5%EB%FC&producer_code[0]=00000000&producer[0]=-&oem[0]=00000000000&year[0]=1989&photo_count[0]=1&id[1]=000000001&code[1]=%C4%C2%C8.091&marka[1]=MITSUBISHI&model[1]=DELICA&modelN[1]=EK8054&kuzovN[1]=P35W&engineN[1]=4D56-T&F_R[1]=-&U_D[1]=-&R_L[1]=-&remark[1]=TEST%20!!!&price[1]=0%20%F0%F3%E1.&id_s_typecurrency[1]=4&N_O[1]=O&name[1]=%E4%E2%E8%E3%E0%F2%E5%EB%FC&producer_code[1]=000000&producer[1]=-&oem[1]=0000000&year[1]=-&photo_count[1]=1

…………………….

&id[39]=000000042&code[39]=%CEC%C2.050&marka[39]=SUBARU&model[39]=LEGACY%20B4&modelN[39]=2SD%20935%2070&kuzovN[39]=BE5&engineN[39]=EJ20&F_R[39]=B&U_D[39]=-&R_L[39]=L&remark[39]=TEST%20!!!%20usa:3157/2sd%20935%20709&price[39]=0%20%F0%F3%E1.&id_s_typecurrency[39]=4&N_O[39]=O&name[39]=%F1%F2%EE%EF&producer_code[39]=2SD%20935%2070&producer[39]=-&oem[39]=-&year[39]=2000&photo_count[39]=0
```

Ответ:

```xml
<?xml version="1.0" encoding="windows-1251"?>
<jcanswer>
    <message>
        <result >SUCCESS</result>
        <action >ins</action>
        <code >N0000_SERVICE_SUCCESS</code>
        <group id="main" area="parts"><![CDATA[Система обработки данных]]></group>
        <datetime >2010-04-15 15:53:43</datetime>
        <text ><![CDATA[Объявления успешно добавлены]]></text>
        <debuginfo ><![CDATA[ … ]]></debuginfo>
    </message>
</jcanswer>
```

Ответ сервера может отличаться в зависимости от того каким образом были получены ссылки, там может быть текстовый ответ, xml или xml + json

Код 1С для добавления товара на сайт. В целом, это общий пример, который покрывает большую часть работы с API сайта, остается только немного менять параметры POST запроса.

[http://1c-cod.ru/code/NVJTSNjzOEbOvmx/](http://1c-cod.ru/code/NVJTSNjzOEbOvmx/)

```
Процедура ДобавитьТоварНаСайтДжапанКар()
    ТелоPOSTЗапроса = "";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&action=ins";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&s_sn=1C_какой_то_там_номер";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&count=1";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&id[0]=УникальныйНомер";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&marka[0]=Toyota";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&model[0]=ipsum";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&kuzov[0]=КакойНомерКузова";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&remark[0]=КомментарийЕслиНужен";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&price[0]=10000000";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&N_O[0]=O"; //N - новая, O - Контрактная
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&name[0]=МегаСуперДворник";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&year[0]=2017";
    ТелоPOSTЗапроса = ТелоPOSTЗапроса + "&photo_count[0]=0";

    Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
    Урл = ПолучитьУрлИзМассива("parts_path=");
    Если Урл = "" Тогда
        Сообщить("Сервер не рассказал, куда идти, продолжать нет смысла");
        Возврат;
    КонецЕсли; 

    РезультатЗапроса = ВыполнитьHTTPЗапрос(Урл, ТелоPOSTЗапроса, Ложь,    Заголовки);

    ЧтениеXML = Новый ЧтениеXML;
    ЧтениеXML.УстановитьСтроку(РезультатЗапроса.ПолучитьТелоКакСтроку());
    ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
    Если ОбъектXDTO.message.result = "SUCCESS" Тогда
        Сообщить("Все хоккей");
    КонецЕсли; 
КонецПроцедуры



&НаКлиенте
Функция ВыполнитьHTTPЗапрос(ПолныйАдресРесурса, ТелоПостЗапроса = "", ЭтоГетЗапрос = Истина, Заголовки = Неопределено)
    Если Заголовки = Неопределено Тогда Заголовки = Новый Соответствие; КонецЕсли;     
    СтруктураURI = СтруктураURI(ПолныйАдресРесурса); 

    HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт); 
    HTTPЗапрос = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере, Заголовки);
    Если ЗначениеЗаполнено(ТелоПостЗапроса) Тогда
        HTTPЗапрос.УстановитьТелоИзСтроки(ТелоПостЗапроса, КодировкаТекста.ANSI);
    КонецЕсли; 

    Попытка
        Если ЭтоГетЗапрос Тогда
            Результат =  HTTPСоединение.Получить(HTTPЗапрос);
        Иначе
            Результат =  HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);
        КонецЕсли; 
    Исключение
        // исключение здесь говорит о том, что запрос не дошел до HTTP-Сервера
        Сообщить("Произошла сетевая ошибка!");
        ВызватьИсключение;
    КонецПопытки;

    Если Результат.КодСостояния >= 400 и Результат.КодСостояния < 500  Тогда
        Сообщить("Код статуса больше 4XX, ошибка запроса.  Код статуса: " + Результат.КодСостояния);
    КонецЕсли;

    Если Результат.КодСостояния >= 500 и Результат.КодСостояния < 600  Тогда
        Сообщить("Код статуса больше 5XX, ошибка сервера. Код статуса: " + Результат.КодСостояния);
    КонецЕсли;

    // Обрабатываем перенаправление
    Если Результат.КодСостояния >= 300 и Результат.КодСостояния < 400  Тогда
        Сообщить("Код статуса больше 3XX, Перенаправление. Код статуса: " + Результат.КодСостояния);
        Если Результат.КодСостояния = 302 Тогда
            Сообщить("Код статуса 302, Постоянное перенаправление.");
            АдресРесурса = Результат.Заголовки.Получить("Location");
            Если АдресРесурса <> Неопределено Тогда
                Сообщить("Выполняю запрос по новому адресу " + АдресРесурса);
                ВыполнитьHTTPЗапрос(АдресРесурса);
            Иначе
                Сообщить("Сервер не сообщил адрес ресурса!");
            КонецЕсли;
        КонецЕсли;
    КонецЕсли;

    Возврат Результат;
КонецФункции

Функция СтруктураURI(Знач СтрокаURI) Экспорт

    СтрокаURI = СокрЛП(СтрокаURI);

    // схема
    Схема = "";
    Позиция = Найти(СтрокаURI, "://");
    Если Позиция > 0 Тогда
        Схема = НРег(Лев(СтрокаURI, Позиция - 1));
        СтрокаURI = Сред(СтрокаURI, Позиция + 3);
    КонецЕсли;

    // строка соединения и путь на сервере
    СтрокаСоединения = СтрокаURI;
    ПутьНаСервере = "";
    Позиция = Найти(СтрокаСоединения, "/");
    Если Позиция > 0 Тогда
        ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
        СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
    КонецЕсли;

    // информация пользователя и имя сервера
    СтрокаАвторизации = "";
    ИмяСервера = СтрокаСоединения;
    Позиция = Найти(СтрокаСоединения, "@");
    Если Позиция > 0 Тогда
        СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
        ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
    КонецЕсли;

    // логин и пароль
    Логин = СтрокаАвторизации;
    Пароль = "";
    Позиция = Найти(СтрокаАвторизации, ":");
    Если Позиция > 0 Тогда
        Логин = Лев(СтрокаАвторизации, Позиция - 1);
        Пароль = Сред(СтрокаАвторизации, Позиция + 1);
    КонецЕсли;

    // хост и порт
    Хост = ИмяСервера;
    Порт = "";
    Позиция = Найти(ИмяСервера, ":");
    Если Позиция > 0 Тогда
        Хост = Лев(ИмяСервера, Позиция - 1);
        Порт = Сред(ИмяСервера, Позиция + 1);
    КонецЕсли;

    Результат = Новый Структура;
    Результат.Вставить("Схема", Схема);
    Результат.Вставить("Логин", Логин);
    Результат.Вставить("Пароль", Пароль);
    Результат.Вставить("ИмяСервера", ИмяСервера);
    Результат.Вставить("Хост", Хост);
    Результат.Вставить("Порт", ?(Порт <> "", Число(Порт), Неопределено));
    Результат.Вставить("ПутьНаСервере", ПутьНаСервере);

    Возврат Результат;

КонецФункции

Функция ПолучитьУрлИзМассива(ТипУрл)
    Если НЕ ПолучитьСписокУРЛ() Тогда Возврат ""; КонецЕсли;

    Для каждого УРЛ Из СписокУРЛ Цикл
        Если Найти(УРЛ, ТипУрл) > 0 Тогда
            Возврат СтрЗаменить(УРЛ, ТипУрл, "")
        КонецЕсли;     
    КонецЦикла; 

    Возврат "";
КонецФункции


Функция ПолучитьСписокУРЛ()
    Если ТипЗнч(СписокУРЛ) = Тип("Массив") И СписокУРЛ.Количество() > 0 Тогда Возврат Истина; КонецЕсли; //все уже инициализировалось

    РезультатЗапроса = ВыполнитьHTTPЗапрос("http://www.japancar.ru/jcgate/gate.php?for=jctrade&object=links&ver=japancar.ru&type=plain_text&answer_version=1.1");
    Если РезультатЗапроса.КодСостояния = 200 Тогда
        СписокУРЛ = ОбщегоНазначения.РазложитьСтрокуВМассивПодстрок(РезультатЗапроса.ПолучитьТелоКакСтроку(), Символы.ПС);
        Если СписокУРЛ[0] = "Ok" И СписокУРЛ.Найти("action=end") > 0 Тогда
            Возврат Истина;
        Иначе
            Сообщить("Не удалось получить список хостов для работы с сайтом");
            Возврат Ложь;
        КонецЕсли; 
    Иначе
        Сообщить("Где то возникла проблема. Код состояния " + РезультатЗапроса.КодСостояния);
        Возврат Ложь;
    КонецЕсли; 
КонецФункции
```



